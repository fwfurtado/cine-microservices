plugins {
    id 'org.springframework.boot' version '2.1.3.RELEASE' apply false
    id 'com.palantir.docker' version '0.21.0' apply false
}

group = 'br.com.caelum'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
}
subprojects {

    repositories {
        mavenLocal()
        mavenCentral()
    }
    def springCloudVersion = 'Greenwich.RELEASE'

    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.palantir.docker'


    group = rootProject.group
    version = rootProject.version
    sourceCompatibility = '1.8'

    bootJar {
        baseName = project.name
        version = project.version
    }

    docker {
        name "$rootProject.name/$project.name"
        dockerfile rootProject.file('Dockerfile')
        dependsOn bootJar
        buildArgs([JAR_FILE: bootJar.archivePath])
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        runtimeOnly 'org.springframework.boot:spring-boot-devtools'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

}
